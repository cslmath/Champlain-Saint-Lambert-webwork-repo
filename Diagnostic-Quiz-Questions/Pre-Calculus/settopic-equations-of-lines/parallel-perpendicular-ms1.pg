## DESCRIPTION
## WeBWorK Diagnostic test question: Pre-Calculus
##     Equations: parallel or perpendicular
##     Multiple Select
## Institution: Champlain College Saint-Lambert
## Authors: Svetla Vassileva, Michele Titcombe 
## WeBWorK Diagnostic Test Project, Winter 2021 - Winter 2022
## ENDDESCRIPTION
#
########## Array and Variable Names ##########
# @allAns, @allT, @allF ... : answer arrays of all possible display choices
# @corrAns, @incorrAns : correct answer array, incorrect answer array
# $numc : number of correct answers to display
# $numi : number of incorrect answers to display
# $showc: toggle to show the correct answer or not
# @slicec : indices of correct answer subset
# @slicei : indices of incorrect answer subset
# @randc : array of correct choice(s) to display
# @randi : array of incorrect choice(s) to display

DOCUMENT();
  loadMacros(
    "PGstandard.pl",
    "PGML.pl",
    "MathObjects.pl",
    "PGchoicemacros.pl",
    "PGcourse.pl",
  );
#TEXT(beginproblem());
TEXT();                         # So no problem number given 
$showPartialCorrectAnswers = 0; # set to 0 because multiple choice

Context("Numeric");

$i = random(0,1,1);
@sign = ("+","-");

@dir = ("parallel","perpendicular");
$n = random(0,1,1);   # n = 1 perpendicular, n = 0 parallel
$parperp = $dir[$n];
$mc = random(2,10,1); 
$bc = $mc + random(2,10,1);
$k = random(2,4,1);
$a = $k*$mc;  # positive
$b =  $a/$mc;  # mc = a/b   -1/mc = -b/a
$numc = random(0,4,1);  # number correct answers up to max 4

$m1 = random(2,10,1);
while ($m1 == $mc){
    $m1 = random(2,10,1);
    }

$b1 = $bc +random(1,5,1);  # positive
$b2 = $b1 + random(1,5,1); # positive

$fc = Compute("$mc x $sign[$i] $bc")->reduce();
# The full list of answers:
@allAns = ("\( y - $mc x = $b1 \)",  #par index 0 
             "\( y =  $mc x + $b1 \)", #par
             "\( $mc x - y = - $b2 \)", #par
             "\(  $a x - $b y + $b1 = 0 \)", #par
             "\( $b y - $a x + $b2 = 0  \)", #par index 4
            "\( $mc y +  x = - $b \)", #perp index 5
             "\(  y = - \frac{1}{$mc} x  \)", #perp
             "\( y + \frac{1}{$mc} x = $b2 \)", #perp
             "\( - $b x - $a y + $mc = 0 \)",   #perp slope is -b/a
             "\( $a y + $b x + $mc = 0 \)",  #perp index 9 
             "\( y - \frac{1}{$mc} x = $mc \)",  #neither index 10
             "\( $a y - $b x + $mc = 0 \)", #neither 11
             "\( y + $mc x = $b1 \)", # neither 12
             "\( y  $sign[$i] \frac{1}{$bc} x = $b1 \)", # neither 13
             "\( $mc x + y = - $b2 \)", # neither 14
             );

@corrAns = ();                  # initialize empty array

if ($n == 0){ # parallel
    @corrAns = @allAns[0..4];
    @incorrAns = @allAns[5..14];  #perpendicular and neither
}
else {
    @incorrAns = @allAns[10..14,0..4];  #parallel and neither
    @corrAns = @allAns[5..9];   
}
@slicec = NchooseK(scalar(@corrAns),$numc);
@corrAns = @corrAns[@slicec];
$numi = 4 - $numc;
@slicei = NchooseK(scalar(@incorrAns),$numi);
@incorrAns = @incorrAns[@slicei];

$quest = ();
$multichoice = new_checkbox_multiple_choice();
if ($numc != 0){
    $multichoice -> qa (
        $quest, 
    @corrAns,
    );
    $multichoice -> extra(
    @incorrAns,
    );
} else { # none of the above
   $multichoice -> qa (
        $quest, 
        "None of the above",
    );
    $multichoice -> extra(
    @incorrAns,
    );
}
$multichoice -> makeLast("None of the above");

BEGIN_PGML
Select the line(s), if any, that are [$parperp] to the line [` y =  [$fc]`].
[@ $multichoice->print_a() @]*  
END_PGML
############################
#  Answer evaluation
install_problem_grader(~~&std_problem_grader);
ANS( checkbox_cmp( $multichoice->correct_ans() ) );

BEGIN_PGML_SOLUTION
Correct answer(s): [@ $multichoice->correct_ans() @]*  
END_PGML_SOLUTION

COMMENT("Diagnostic test question. Do not assign for homework.");
ENDDOCUMENT();