## DESCRIPTION
## WeBWorK Diagnostic test question: Pre-Calculus
##     Domain (interval notation):  square root (infinite interval)
##     Multiple Choice
## Institution: Champlain College Saint-Lambert
## Authors: Svetla Vassileva, Michele Titcombe 
## WeBWorK Diagnostic Test Project, Winter 2021 - Winter 2022
## ENDDESCRIPTION
#
########## Array and Variable Names ##########
# @allAns, @allT, @allF ... : answer arrays of all possible display choices
# @corrAns, @incorrAns : correct answer array, incorrect answer array
# $numc : number of correct answers to display
# $numi : number of incorrect answers to display
# $showc: toggle to show the correct answer or not
# @slicec : indices of correct answer subset
# @slicei : indices of incorrect answer subset
# @randc : array of correct choice(s) to display
# @randi : array of incorrect choice(s) to display

DOCUMENT();
  loadMacros(
    "PGstandard.pl",
    "PGML.pl",
    "MathObjects.pl",
    "parserRadioButtons.pl",
    "PGchoicemacros.pl",
    "PGcourse.pl",
  );
#TEXT(beginproblem());
TEXT(); # So no problem number given 
$showPartialCorrectAnswers = 0;   # set to 0 because multiple choice

Context("Numeric");
# set parameters
$a = random(-11,11,1);
$c = random(0,3,1); # decides which of 4 functions to display of array @fun

# computations
$ap1 = $a + 1;
$am1 = $a - 1;
@fun = ("sqrt(x-$a)","1/sqrt(x-$a)","sqrt($a - x)","1/sqrt($a - x )");
$f = Compute($fun[$c])->reduce();

# array of possible intervals to display
@allAns = ("\( [$a,\infty) \)", 
          "\( ($a,\infty) \)", 
          "\( (-\infty,$a] \)", 
          "\( (-\infty,$a) \)", 
          "\( (-\infty, $ap1] \)", # extras incorrect
          "\( (-\infty,$ap1) \)", 
          "\( ($am1,\infty) \)", 
          "\( [$am1,\infty) \)", 
          "\( [$ap1,\infty) \)", 
          "\( ($ap1,\infty) \)", 
          "\( (-\infty, $am1] \)",
          "\( (-\infty, $am1) \)",
);

# set the correct answer and array of incorrect answers
@corrAns = (); 
$correct = $allAns[$c];
push(@corrAns,$correct);
%yes = map{$_=>1} @corrAns;
@incorrAns = grep(!defined $yes{$_}, @allAns);

# Show the correct answer more often than not
$showc = random(0,9,1);
$numc = 1; # show the correct answer
if ($showc == 0){$numc = 0;} # don't show the correct answer

# choose how many and which incorrect answers to display
$numi = 4 - $numc;   # the 4 is the total number of choices to display
@slicei = NchooseK(scalar(@incorrAns),$numi);
@randi = @incorrAns[@slicei];

if ($numc != 0){  # at least one correct answer
    $radio1 = RadioButtons([[$correct,@randi],
    "None of the above"],0);  # correct index 0    
    }
else {            # no correct answers
    $radio1 = RadioButtons([[@randi,],"None of the above",],4);  # correct index 4
}

BEGIN_PGML
Select the interval, if any, that describes the domain of the function [``f(x)=[$f]``].  
[@ $radio1->buttons() @]*
END_PGML
############################
#  Answer evaluation
install_problem_grader(~~&std_problem_grader);
ANS( $radio1->cmp( ) );

BEGIN_PGML_SOLUTION
Correct answer: [@ $radio1->correct_ans() @]*
END_PGML_SOLUTION

COMMENT("Diagnostic test question. Do not assign for homework.");
ENDDOCUMENT();
